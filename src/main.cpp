#include <Arduino.h>
#include <WiFi.h>
#include <WiFiClientSecure.h>
#include <UniversalTelegramBot.h>
#include <time.h>

#include "Config.hpp"
#include "LockController.hpp"
#include "TelegramLockBot.hpp"
#include "AccessManager.hpp"
#include "DoorSensor.hpp"
#include "Buzzer.hpp"
#include "FingerprintAuth.hpp"
#include "SafeCamera.hpp"
#include "RfidReader.hpp"
#include "MotionSensor.hpp"

HardwareSerial FingerSerial(2);

WiFiClientSecure secured_client;
UniversalTelegramBot bot(BOT_TOKEN, secured_client);

SafeCamera safeCamera(bot);
LockController lockController(LOCK_PIN, LOCK_OPEN_TIME);
DoorSensor doorSensor(DOOR_SENSOR_PIN, DOOR_OPEN_LEVEL);
Buzzer buzzer(BUZZER_PIN, BUZZER_ACTIVE_LEVEL);
RfidReader rfid;
MotionSensor motionSensor;

AccessManager accessManager(
    ADMIN_CHAT_IDS,  NUM_ADMIN_CHATS,
    USER_CHAT_IDS,   NUM_USER_CHATS,
    VIEWER_CHAT_IDS, NUM_VIEWER_CHATS
);

TelegramLockBot telegramLockBot(
    bot,
    lockController,
    doorSensor,
    accessManager,
    buzzer,
    BOT_UPDATE_INTERVAL
);

FingerprintAuth fingerprintAuth(
    FingerSerial,
    33,
    32,
    lockController,
    buzzer,
    &bot,
    (NUM_ADMIN_CHATS > 0 ? ADMIN_CHAT_IDS[0] : nullptr)
);

void connectWiFi() {
    Serial.print("–ü–æ–¥–∫–ª—é—á–µ–Ω–∏–µ –∫ Wi-Fi: ");
    Serial.println(WIFI_SSID);

    WiFi.mode(WIFI_STA);
    WiFi.begin(WIFI_SSID, WIFI_PASSWORD);

    uint8_t tryCount = 0;
    while (WiFi.status() != WL_CONNECTED) {
        delay(500);
        Serial.print(".");
        if (++tryCount == 60) {
            Serial.println("\n–ù–µ —É–¥–∞–ª–æ—Å—å –ø–æ–¥–∫–ª—é—á–∏—Ç—å—Å—è –∫ Wi-Fi, –ø–µ—Ä–µ–∑–∞–≥—Ä—É–∑–∫–∞...");
            ESP.restart();
        }
    }

    Serial.println("\nWi-Fi –ø–æ–¥–∫–ª—é—á–µ–Ω!");
    Serial.print("IP: ");
    Serial.println(WiFi.localIP());
}

void setupTimeAndTLS() {
    Serial.println("–°–∏–Ω—Ö—Ä–æ–Ω–∏–∑–∞—Ü–∏—è –≤—Ä–µ–º–µ–Ω–∏ —á–µ—Ä–µ–∑ NTP...");
    configTime(0, 0, "pool.ntp.org", "time.nist.gov", "time.google.com");

    time_t now = time(nullptr);
    int retries = 0;
    while (now < 24 * 3600 && retries < 30) {
        delay(500);
        Serial.print(".");
        now = time(nullptr);
        retries++;
    }
    Serial.println();

    if (now < 24 * 3600) {
        Serial.println("‚ö†Ô∏è –ù–µ —É–¥–∞–ª–æ—Å—å —Å–∏–Ω—Ö—Ä–æ–Ω–∏–∑–∏—Ä–æ–≤–∞—Ç—å –≤—Ä–µ–º—è, TLS –º–æ–∂–µ—Ç –Ω–µ —Ä–∞–±–æ—Ç–∞—Ç—å.");
    } else {
        Serial.print("–¢–µ–∫—É—â–µ–µ –≤—Ä–µ–º—è (UTC): ");
        Serial.println(ctime(&now));
    }

    secured_client.setInsecure();
    Serial.println("TLS: —É—Å—Ç–∞–Ω–æ–≤–ª–µ–Ω –∫–æ—Ä–Ω–µ–≤–æ–π —Å–µ—Ä—Ç–∏—Ñ–∏–∫–∞—Ç Telegram.");
}

void setup() {
    Serial.begin(115200);
    delay(1000);

    pinMode(LOCK_PIN, OUTPUT);
    digitalWrite(LOCK_PIN, LOW);

    pinMode(BUZZER_PIN, OUTPUT);
    digitalWrite(BUZZER_PIN, !BUZZER_ACTIVE_LEVEL);

    Serial.println();
    Serial.println("[BOOT] –°—Ç–∞—Ä—Ç —É—Å—Ç—Ä–æ–π—Å—Ç–≤–∞ —É–º–Ω–æ–≥–æ –∑–∞–º–∫–∞");

    lockController.begin();
    doorSensor.begin();
    buzzer.begin();
    fingerprintAuth.begin();

    connectWiFi();
    setupTimeAndTLS();

    telegramLockBot.begin();

    bool rfidOk = rfid.begin();
    if (!rfidOk) {
        Serial.println("[RFID] PN532 –Ω–µ –∏–Ω–∏—Ü–∏–∞–ª–∏—Ä–æ–≤–∞–Ω, RFID –ø–æ–∫–∞ —Ä–∞–±–æ—Ç–∞—Ç—å –Ω–µ –±—É–¥–µ—Ç.");
    }

    bool accOk = motionSensor.begin();
    if (!accOk) {
        Serial.println("[ACC] –ê–∫—Å–µ–ª–µ—Ä–æ–º–µ—Ç—Ä –Ω–µ –∏–Ω–∏—Ü–∏–∞–ª–∏–∑–∏—Ä–æ–≤–∞–ª—Å—è, –¥–∞—Ç—á–∏–∫ –¥–≤–∏–∂–µ–Ω–∏—è –Ω–µ —Ä–∞–±–æ—Ç–∞–µ—Ç.");
    }

    bool camOk = safeCamera.begin();
    if (!camOk) {
        Serial.println("[CAM] –ö–∞–º–µ—Ä–∞ –Ω–µ –∏–Ω–∏—Ü–∏–∞–ª–∏–∑–∏—Ä–æ–≤–∞–ª–∞—Å—å, /photo –±—É–¥–µ—Ç –≤—ã–¥–∞–≤–∞—Ç—å –æ—à–∏–±–∫—É.");
    }

    if (NUM_ADMIN_CHATS > 0) {
        bot.sendMessage(ADMIN_CHAT_IDS[0],
                        "‚úÖ –ë–æ—Ç —É–º–Ω–æ–≥–æ –∑–∞–º–∫–∞ –∑–∞–ø—É—â–µ–Ω.\n"
                        "–£—Å—Ç—Ä–æ–π—Å—Ç–≤–æ –æ–Ω–ª–∞–π–Ω –∏ –≥–æ—Ç–æ–≤–æ –∫ —Ä–∞–±–æ—Ç–µ.",
                        "");
    }
}

void loop() {
    if (motionSensor.loop(1.5f)) {
        telegramLockBot.notifyAdmins(
            "üö® –û–±–Ω–∞—Ä—É–∂–µ–Ω–æ —Å–∏–ª—å–Ω–æ–µ –¥–≤–∏–∂–µ–Ω–∏–µ/—É–¥–∞—Ä –ø–æ —Å–µ–π—Ñ—É!\n"
            "–ü—Ä–æ–≤–µ—Ä—å—Ç–µ, –≤—Å—ë –ª–∏ –≤ –ø–æ—Ä—è–¥–∫–µ."
        );
    }
    lockController.update();
    buzzer.update();
    fingerprintAuth.update();
    telegramLockBot.update();

    String uid;
    if (rfid.readCard(uid)) {
        Serial.print("[RFID] UID –∫–∞—Ä—Ç—ã: ");
        Serial.println(uid);

        if (rfid.isAuthorized(uid)) {
            Serial.println("[RFID] –ê–≤—Ç–æ—Ä–∏–∑–æ–≤–∞–Ω–Ω–∞—è –∫–∞—Ä—Ç–∞, –æ—Ç–∫—Ä—ã–≤–∞–µ–º –∑–∞–º–æ–∫.");

            if (!lockController.isOpen()) {
                lockController.open();
                buzzer.beep(2);
            } else {
                Serial.println("[RFID] –ó–∞–º–æ–∫ —É–∂–µ –æ—Ç–∫—Ä—ã—Ç.");
            }
        } else {
            Serial.println("[RFID] –ù–ï–∞–≤—Ç–æ—Ä–∏–∑–æ–≤–∞–Ω–Ω–∞—è –∫–∞—Ä—Ç–∞.");
            // –ú–æ–∂–Ω–æ —Ç—É—Ç —Å–¥–µ–ª–∞—Ç—å –∫–æ—Ä–æ—Ç–∫–∏–π –æ–¥–∏–Ω –ø–∏—Å–∫ –∫–∞–∫ ‚Äú–æ—à–∏–±–∫–∞‚Äù
            // buzzer.beep(1);
        }
    }

    delay(5);
}
